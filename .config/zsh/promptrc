#!/usr/bin/zsh

# VCS info
autoload -Uz vcs_info
precmd() { vcs_info }
setopt prompt_subst
zstyle ':vcs_info:*' enable git svn
zstyle ':vcs_info:git:*' formats "%{$fg[magenta]%}%s:(%{$fg[green]%}%b%{$fg[magenta]%})%{$reset_color%} "

function set-prompt () {
    case ${KEYMAP} in
        (vicmd)      VI_MODE="%{$fg[red]%}(N)" ;;
        (main|viins) VI_MODE="%{$fg[green]%}(I)" ;;
        (*)          VI_MODE="%{$fg[green]%}(I)" ;;
    esac

    # fish-shell's shashimi like prompt
    PROMPT='%B$VI_MODE %{$fg[green]%}%(?:◆ :%{$fg[red]%}✖%{$fg[green]%} $? )%~ ${vcs_info_msg_0_}%{$fg[green]%}%(?:%{$fg[magenta]%}❯%{$fg[green]%}:❯)❯❯%{$reset_color%}%b '
}

function zle-line-init zle-keymap-select {
    set-prompt
    zle reset-prompt
}

zle -N zle-line-init
zle -N zle-keymap-select
